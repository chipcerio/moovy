apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount'

def apiKeysPropertiesFile = rootProject.file("apikeys.properties")
def apiKeysProperties = new Properties()
apiKeysProperties.load(new FileInputStream(apiKeysPropertiesFile))

// https://blog.jetbrains.com/kotlin/2017/08/kotlin-1-1-4-is-out
androidExtensions {
    experimental = true
}

// https://blog.jetbrains.com/kotlin/2018/01/kotlin-1-2-20-is-out/
kapt {
    useBuildCache = true
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.2"
    defaultConfig {
        applicationId "com.chipcerio.moovy"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main.java.srcDirs = ['src/main/kotlin']
        test.java.srcDirs = ['src/test/kotlin']
    }

    buildTypes {
        release {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'
            debuggable true
            minifyEnabled false
        }
    }

    flavorDimensions "default"

    productFlavors {
        demo {
            applicationIdSuffix ".demo"
            dimension "default"
        }
        full {
            applicationIdSuffix ".full"
            dimension "default"
        }
    }

    // mockRelease is never needed
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release' && variant.getFlavors().get(0).name == 'demo') {
            variant.setIgnore(true)
        }

        // TODO remove upon release
        if (variant.buildType.name == 'release' && variant.getFlavors().get(0).name == 'full') {
            variant.setIgnore(true)
        }
    }

    buildTypes.each {
        it.buildConfigField('String', 'TRAKT_CLIENT_ID', apiKeysProperties['TraktClientId'])
        it.buildConfigField('String', 'TRAKT_CLIENT_SECRET', apiKeysProperties['TraktClientSecret'])
        it.buildConfigField('String', 'TMDB_API_KEY', apiKeysProperties['TmdbApiKey'])
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // support library
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'

    // persistence
    implementation 'android.arch.persistence.room:runtime:1.0.0'
    kapt 'android.arch.persistence.room:compiler:1.0.0'
    implementation 'android.arch.persistence.room:rxjava2:1.0.0'

    // reactive extensions
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'

    // network layer
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    // image loading
    implementation 'com.github.bumptech.glide:glide:4.1.1'
    kapt 'com.github.bumptech.glide:compiler:4.1.1'

    // dependency injection
    implementation 'com.google.dagger:dagger:2.11'
    implementation 'com.google.dagger:dagger-android-support:2.11'
    kapt 'com.google.dagger:dagger-compiler:2.11'
    kapt 'com.google.dagger:dagger-android-processor:2.11'
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    // utilities
    implementation 'com.jakewharton.timber:timber:4.5.1'
}
